*********************************************************************
*
* Door stage init
*
door_stage_init:
       .proc
       li   r1,head_and_hands_patterns
       bl   @load_sprite_patterns
       li   r1,character_set_door
       bl   @set_character_set
       clr  @max_residents
       .endproc
*// door_stage_init

*********************************************************************
*
* Base stage init
*
base_stage_init:
       .proc
       li   r1,character_set_base
       bl   @set_character_set
       .endproc
*// base_stage_init

*********************************************************************
*
* One resident stage init
*
one_resident_stage_init:
       .proc
       li   r1,character_set_residents
       bl   @set_character_set
       li   r1,head_and_hands_patterns
       bl   @load_sprite_patterns
       li   r0,1
       mov  r0,@max_residents
       .endproc
*// one_resident_stage_init

*********************************************************************
*
* Two residents stage init
*
two_residents_stage_init:
       .proc
       li   r1,head_and_hands_patterns
       bl   @load_sprite_patterns
       li   r1,head_and_hands_patterns
       bl   @load_sprite_patterns
       li   r0,2
       mov  r0,@max_residents
       .endproc
*// two_residents_stage_init

*********************************************************************
*
* Three residents stage init
*
three_residents_stage_init:
       .proc
       li   r1,character_set_residents
       bl   @set_character_set
       li   r1,head_and_hands_patterns
       bl   @load_sprite_patterns
       li   r0,3
       mov  r0,@max_residents
       .endproc
*// three_residents_stage_init

*********************************************************************
*
* Griders and dumbbells stage init
*
girders_stage_init:
       .proc
       li   r1,girder_dumbbell_patterns
       bl   @load_sprite_patterns
       li   r1,character_set_base
       bl   @set_character_set
       bl   @create_girder
       bl   @create_girder
       .endproc
*// girders_stage_init

*********************************************************************
*
* Bird stage init
*
bird_stage_init:
       .proc
       li   r1,bird_patterns
       bl   @load_sprite_patterns
       bl   @allocate_sprite
       mov  r4,r4
       jeq  bird_stage_init_1
       li   r0,bird_init_data
       mov  *r0+,@sprite_handler(r4)
       mov  *r0+,@sprite_x(r4)
       mov  *r0+,@sprite_y(r4)
       mov  *r0+,@sprite_vx(r4)
       mov  *r0+,@sprite_vy(r4)
       mov  *r0+,@sprite_count(r4)
       clr  @bird_droppings
       clr  @bird_rounds
       li   r0,bird_module
       bl   @play_module
bird_stage_init_1:
       .endproc
*// bird_stage_init

*********************************************************************
*
* Kong stage init
*
kong_stage_init:
       .proc
       li   r1,kong_patterns
       bl   @load_sprite_patterns
       li   r1,character_set_kong
       bl   @set_character_set
       bl   @allocate_sprite
       mov  r4,r4
       jeq  kong_stage_init_1
       li   r0,kong_init_data
       mov  *r0+,@sprite_handler(r4)
       mov  *r0+,@sprite_x(r4)
       mov  *r0+,@sprite_y(r4)
       mov  *r0+,@sprite_count(r4)
       mov  *r0+,@sprite_ax(r4)        ; Used to store current arms
       mov  *r0+,@sprite_ay(r4)        ; Used to store current side (left = 0, right <> 0)
       li   r0,kong_map
       li   r1,map_buffer+(kong_row_1_3*map_width)+3
       bl   @copy_kong_to_map
       li   r0,kong_module
       bl   @play_module
kong_stage_init_1:
       .endproc
*// kong_stage_init

*********************************************************************
*
* Sparking sign stage init
*
sparking_sign_stage_init_level_2:
       .proc
       li   r4,sparking_sign_init_data_2
       jmp  sparking_sign_stage_init
sparking_sign_stage_init_level_4:
       .proc
       li   r4,sparking_sign_init_data_4
sparking_sign_stage_init:
       li   r1,spark_patterns
       bl   @load_sprite_patterns
       li   r1,character_set_sign
       bl   @set_character_set
       mov  r4,r0
       li   r2,2
sparking_sign_stage_init_1:
       bl   @allocate_sprite
       mov  r4,r4
       jeq  sparking_sign_stage_init_2
       mov  *r0+,@sprite_handler(r4)
       mov  *r0+,@sprite_x(r4)
       mov  *r0+,@sprite_y(r4)
       mov  *r0+,@sprite_count(r4)
       mov  *r0+,@sprite_ax(r4)        ; Used to store patterns
       mov  *r0+,@sprite_ay(r4)        ; Spark ball x offset
       dec  r2
       jne  sparking_sign_stage_init_1
sparking_sign_stage_init_2:
       bl   @copy_sign_to_map
       clr  @spark_hits
       .endproc
*//  sparking_sign_stage_init

*********************************************************************
*
* Chopper stage init
*
chopper_stage_init:
       .proc
       li   r1,chopper_patterns
       bl   @load_sprite_patterns
       bl   @allocate_sprite
       mov  r4,r4
       jeq  chopper_stage_init_1
       li   r0,chopper_init_data
       mov  *r0+,@sprite_handler(r4)
       mov  *r0+,@sprite_x(r4)
       mov  *r0+,@sprite_y(r4)
       mov  *r0+,@sprite_count(r4)
       mov  *r0+,@sprite_vx(r4)
       mov  *r0+,@sprite_vy(r4)
chopper_stage_init_1:
       .endproc
*// chopper_stage_init


